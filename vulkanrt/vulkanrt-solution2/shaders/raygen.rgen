#version 460
#extension GL_EXT_ray_tracing : require

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D image;
layout(binding = 2, set = 0) uniform CameraProperties 
{
	mat4 viewInverse;
	mat4 projInverse;
	vec4 lightPos;
} cam;


struct RayPayload {
	vec3 color;
	float distance;
	vec3 normal;
	float reflector;
};

layout(location = 0) rayPayloadEXT RayPayload rayPayload;

// Max. number of recursion is passed via a specialization constant
layout (constant_id = 0) const int MAX_RECURSION = 0;

void main() 
{
	const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
	vec2 d = inUV * 2.0 - 1.0;

	vec4 origin = cam.viewInverse * vec4(0,0,0,1);
	vec4 target = cam.projInverse * vec4(d.x, d.y, 1, 1) ;
	vec4 direction = cam.viewInverse*vec4(normalize(target.xyz / target.w), 0);

	uint rayFlags = gl_RayFlagsOpaqueEXT;
	uint cullMask = 0xff;
	float tmin = 0.001;
	float tmax = 10000.0;

	vec3 color = vec3(0.0);

	for (int i = 0; i < MAX_RECURSION; i++) {
		traceRayEXT(topLevelAS, rayFlags, cullMask, 0, 0, 0, origin.xyz, tmin, direction.xyz, tmax, 0);
		vec3 hitColor = rayPayload.color;

		if (rayPayload.distance < 0.0f) {
			color += hitColor;
			break;
		} else if (rayPayload.reflector == 1.0f) {
			// Calculate hitposition of current intersect
            const vec4 hitPos = origin + direction * rayPayload.distance;

			// Select correct refrEta and refrNormal by evaluating angle between surface normal versus ray direction (figure out wether we are entering or leaving the medium)
            const float NdotD = dot(rayPayload.normal, direction.xyz);
            const float refractionIndex = 1.0f/1.05f;
            vec3 refrNormal = rayPayload.normal;
            float refrEta;

            if(NdotD > 0.0f) {
                refrNormal = -rayPayload.normal;
                refrEta = 1.0f / refractionIndex;
            } else {
                refrNormal = rayPayload.normal;
                refrEta = refractionIndex;
            }

			// Setting new ray origin slightly further ahead to prevent "looping" the same intersection over and over again
            origin = hitPos + direction * 0.001f;
			// Calculate next ray direction by using GLSL 'refract' function
            direction.xyz = refract(direction.xyz, refrNormal, refrEta);
		} else {
			color += hitColor;
			break;
		}

	}

	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(color, 0.0));
}
